# Работа с заявками клиентов

## Договоры на абонентское обслуживание

Необходимо добавить новый вид договоров – абоненское обслуживание. В договорах такого вида пользователь должен иметь возможность внести дату начала действия договора, дату окончания действия договора, сумму ежемесячной абоненсткой платы и стоимость часа работы специалиста.

## Обслуживание клиентов

Необходимо планировать обслуживание сотрудниками оборудования и программ клиентов. 

Процесс работы с заявками построен следующим образом:
1. Клиент звонит менеджеру и оставляет заявку на работу специалиста с указанием проблемы, для решения которой нужен специалист.
2. Менеджер ищет свободное время и планирует заявку на это время.
3. При планировании телеграм-бот оповещает специалистов о появлении новой заявки.
4. Специалист в назначенное время в офисе клиента или удалённо проводит необходимые работы.
5. Специалист получает подписанный лист учёта рабочего времени или скан/фото листа учёта от клиента. В листе учёта перечисляются виды работ, выполненные специалистом, и фиксируется количество часов к оплате. В дальнейшем документ будет являться подтверждением проведения работ.
6. Специалист вносит в информационную систему информацию о выполненных работах, количество фактически потраченных часов на каждый вид работы, количество часов к оплате клиенту за каждый вид работы и прикрепляет к документу скан/фото листа учёта рабочего времени.

Все объекты, связанные с обслуживанием клиентов, должны располагаться в новой подсистеме Обслуживание клиентов.

## Реализованное решение
1. Добавлен документ ОбслуживаниеКлиента с реквизитами:
    - Клиент
    - Договор
    - Специалист
    - Дата проведения работ
    - Время начала работ (план)
    - Время окончания работ (план)
    - Описание проблемы
    - Комментарий
2. В документ добавлена табличная часть ВыполненныеРаботы с колонками:
    - Описание работ
    - Фактически потрачено часов
    - Часы к оплате клиенту
3. Реализована форма документа и форма списка с разумным размещением полей, подключена к формам подсистема Подключаемые команды
4. Добавлена возможность хранить присоединенные к документу файлы
5. Создана роль для работы с документом
6. Добавлен регистр накопления для хранения информации о суммах задолженности клиента ВыполненныеКлиентуРаботы
    - Измерения - Клиент, Договор
    - Ресурсы - Количество часов, Сумма к оплате
7. Реализована проверка при проведении документа, что выбран договор с типом абонентская плата и что дата документа лежит между датой начала и датой окончания действия договора

<details>
	
 <summary><u>Код проверки при проведении документа</u></summary>
 
```bsl
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записать();
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Дата КАК Дата,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_ДатаНачала КАК ДатаНачалаДоговора,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_ДатаОкончания КАК ДатаОкончанияДоговора,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВидДоговора КАК ВидДоговора,
		|	СУММА(ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.ЧасыКОплатеКлиенту) КАК КоличествоЧасов,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Клиент КАК Клиент,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор КАК Договор,
		|	СУММА(ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Сумма) КАК СуммаКОплате,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Специалист КАК Сотрудник,
		|	МАКСИМУМ(ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот) КАК ПроцентОтРабот,
		|	МАКСИМУМ(ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_СтоимостьЧасаРаботы) КАК СтоимостьЧасаРаботы,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_СуммаАбонентскойПлаты КАК СуммаАбонентскойПлаты
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента.ВыполненныеРаботы КАК ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, Сотрудник В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВКМ_ОбслуживаниеКлиента.Специалист
		|			ИЗ
		|				Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Специалист = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Дата,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_ДатаНачала,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_ДатаОкончания,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВидДоговора,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Клиент,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Специалист,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_СуммаАбонентскойПлаты";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
		Если ВыборкаДетальныеЗаписи.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда 
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Вид договора не является абонентским обслуживанием");
			Прервать;
		КонецЕсли;
		
		Если ВыборкаДетальныеЗаписи.Дата > КонецДня(ВыборкаДетальныеЗаписи.ДатаОкончанияДоговора)
			Или ВыборкаДетальныеЗаписи.Дата < ВыборкаДетальныеЗаписи.ДатаНачалаДоговора
			Тогда
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Дата документа должна быть в границах действия договора на абоненсткое обслуживание!");
			Прервать;
		КонецЕсли;	
		
		Если ПустаяСтрока(ВыборкаДетальныеЗаписи.ПроцентОтРабот) Тогда
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Специалисту не установлен процент оплаты от работ!");
			Прервать;
		КонецЕсли;	
		
		//регистр ВКМ_ВыполненныеКлиентуРаботы
		Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
		Движение.Период = Дата;
		
		//регистр ВКМ_ВыполненныеСотрудникомРаботы
		Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
		Движение.Период = Дата;
		Движение.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
		Движение.ЧасовКОплате = ВыборкаДетальныеЗаписи.КоличествоЧасов;
		Движение.Процент = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ВыборкаДетальныеЗаписи.ПроцентОтРабот);
		Движение.СуммаКОплате = ВыборкаДетальныеЗаписи.КоличествоЧасов * ВыборкаДетальныеЗаписи.СтоимостьЧасаРаботы * Движение.Процент / 100;

			
	КонецЦикла;


КонецПроцедуры
```
</details>

8. Реализована обработка проведения по регистру накопления. Сумма к оплате рассчитывается исходя из ставки часа, указанной в договоре
9. Добавлен документ в подсистему Обслуживание клиентов
10. Для оповещения через Телеграм созданы константы:
    - токен управления телеграм-ботом
    - идентификатор группы для оповещения
11. Реализован отдельный общий модуль для интеграции с телеграм-ботом (ВКМ_ИнтеграцияТГ)

<details>
	
 <summary><u>Общий модуль для интеграции с телеграм-ботом</u></summary>

```bsl
    #Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ОтправитьСообщение () Экспорт

	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ИдентификторГруппыДляОповещения.Значение КАК Chat_id,
		|	ВКМ_ТокенУправленияТГБотом.Значение КАК TokenTG,
		|	ВКМ_УведомленияТГБот.Ссылка,
		|	ВКМ_УведомленияТГБот.Текст
		|ИЗ
		|	Константа.ВКМ_ТокенУправленияТГБотом КАК ВКМ_ТокенУправленияТГБотом,
		|	Константа.ВКМ_ИдентификторГруппыДляОповещения КАК ВКМ_ИдентификторГруппыДляОповещения,
		|	Справочник.ВКМ_УведомленияТГБот КАК ВКМ_УведомленияТГБот";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	
	Пока Выборка.Следующий () Цикл
		TokenTG = Выборка.TokenTG;
		Chat_id = Выборка.Chat_id;
		
		Результат = ОтправкаЗаявкиТГ(Выборка.Текст, TokenTG, Chat_id);
			Если Результат Тогда
				Объект = Выборка.Ссылка.ПолучитьОбъект();
				Объект.Удалить();
			КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ОтправкаЗаявкиТГ (Текст, Токен, ЧатИД)
		
		АдресТГ = "api.telegram.org";
		Соединение  =  Новый HTTPСоединение(АдресТГ,443,,,,,Новый ЗащищенноеСоединениеOpenSSL());
		
		Данные = Новый Структура();
		Данные.Вставить("text", Текст);
		
				
		ПараметрыЗаписи = Новый ПараметрыЗаписиJSON(, Символы.Таб);
		
		Запись = Новый ЗаписьJSON;
		Запись.УстановитьСтроку(ПараметрыЗаписи);
		ЗаписатьJSON(Запись, Данные);
		СтрокаJSON = Запись.Закрыть();
		
		Заголовки = Новый Соответствие();
		Заголовки.Вставить("Content-Type", "application/json");
		АдресЗапроса = "bot" + Токен + "/sendMessage?chat_id=" + Формат(ЧатИД, "ЧГ=");
		
		Запрос = Новый HTTPЗапрос(АдресЗапроса, Заголовки);
		Запрос.УстановитьТелоИзСтроки(СтрокаJSON);
		
		Попытка
			Результат = Соединение.ОтправитьДляОбработки(Запрос);
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операции'"),
			УровеньЖурналаРегистрации.Ошибка, , ,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
		Если Результат.КодСостояния = 200 Тогда
			Возврат Истина;
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецФункции

#КонецОбласти
#КонецЕсли
```
</details>

13. Создан справочник Уведомления телеграм-боту для отправки с нулевой длиной кода и наименования и реквизитом Текст сообщения (строка бесконечной длины)
14. При записи документа Обслуживание клиента, если документ записывается первый раз или если дата, время или специалист изменились, добавлено в справочник сообщение с текстом, описывающим изменения. Модуль объекта:

<details>
	
 <summary><u>Модуль объекта документа Обслуживание клиента</u></summary>

```bsl
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ОбъектУведомленияТГ = Справочники.ВКМ_УведомленияТГБот.СоздатьЭлемент();
		ОбъектУведомленияТГ.Текст = СтрШаблон("Создана новая заявка от %1 для обслуживания клиента %2", Дата, Клиент);
		ОбъектУведомленияТГ.Записать();

	КонецЕсли;

	ДатаДокумента = '00010101';
	ДатаПроведенияРаботДок = '00010101';
	ВремяНачалаРаботПланДок =  '00010101000000';
	ВремяОкончанияРаботПланДок =  '00010101000000';
	СпециалистДок = " ";
	
	Если Не ЭтоНовый() Тогда
	 Запрос = Новый Запрос;
	 Запрос.Текст =
	 	"ВЫБРАТЬ
	 	|	ВКМ_ОбслуживаниеКлиента.Дата КАК ДатаДокумента,
	 	|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот,
	 	|	ВКМ_ОбслуживаниеКлиента.ВремяНачалаРаботПлан,
	 	|	ВКМ_ОбслуживаниеКлиента.ВремяОкончанияРаботПлан,
	 	|	ВКМ_ОбслуживаниеКлиента.Специалист
	 	|ИЗ
	 	|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
	 	|ГДЕ
	 	|	ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка";
	 
	 Запрос.УстановитьПараметр("Ссылка", Ссылка);
	 
	 РезультатЗапроса = Запрос.Выполнить();
	 
	 Выборка = РезультатЗапроса.Выбрать();
	 
	 Если Выборка.Следующий() Тогда
	 	ДатаДокумента = Выборка.ДатаДокумента;
	 	ДатаПроведенияРаботДок = Выборка.ДатаПроведенияРабот;
	 	ВремяНачалаРаботПланДок = Выборка.ВремяНачалаРаботПлан;
	 	ВремяОкончанияРаботПланДок = Выборка.ВремяОкончанияРаботПлан;
	 	СпециалистДок = Выборка.Специалист; 
	 КонецЕсли;

	Если ДатаДокумента <> Дата Тогда
		ОбъектУведомленияТГ = Справочники.ВКМ_УведомленияТГБот.СоздатьЭлемент();
		ОбъектУведомленияТГ.Текст = СтрШаблон("Изменена дата в заявке № %1. Новая дата: %2", Номер, Дата);
		ОбъектУведомленияТГ.Записать();

	КонецЕсли;
	
	Если ДатаПроведенияРаботДок <> ДатаПроведенияРабот Тогда 	 
		ОбъектУведомленияТГ = Справочники.ВКМ_УведомленияТГБот.СоздатьЭлемент();
		ОбъектУведомленияТГ.Текст = СтрШаблон("Изменена дата проведения работ в заявке № %1. Новая дата проведения работ: %2", Номер, ДатаПроведенияРабот);
		ОбъектУведомленияТГ.Записать();
		
	КонецЕсли;
	
	Если ВремяНачалаРаботПланДок <> ВремяНачалаРаботПлан Тогда 	
		ОбъектУведомленияТГ = Справочники.ВКМ_УведомленияТГБот.СоздатьЭлемент();
		ОбъектУведомленияТГ.Текст = СтрШаблон("Изменено плановое время начала работ в заявке № %1. Новое плановое время: %2", Номер, Формат(ВремяНачалаРаботПлан,"ДЛФ=T;"));
		ОбъектУведомленияТГ.Записать();
	КонецЕсли;
	
	Если ВремяОкончанияРаботПланДок <> ВремяОкончанияРаботПлан Тогда
		ОбъектУведомленияТГ = Справочники.ВКМ_УведомленияТГБот.СоздатьЭлемент();
		ОбъектУведомленияТГ.Текст = СтрШаблон("Изменено плановое время окончания работ в заявке № %1. Новое плановое время: %2", Номер, Формат(ВремяОкончанияРаботПлан,"ДЛФ=T;"));
		ОбъектУведомленияТГ.Записать();
	КонецЕсли;
	
	Если СпециалистДок <> Специалист Тогда
		ОбъектУведомленияТГ = Справочники.ВКМ_УведомленияТГБот.СоздатьЭлемент();
		ОбъектУведомленияТГ.Текст = СтрШаблон("Изменен специалист в заявке № %1. Новый специалист: %2", Номер, Специалист);
		ОбъектУведомленияТГ.Записать();
	КонецЕсли;
	
	КонецЕсли;
	

КонецПроцедуры


#КонецОбласти
#КонецЕсли
```
</details>

15. Добавлено регламентное задание, которое обходит элементы справочника Уведомления, отправляет и после успешной отправки удаляет элементы справочника.
![image](https://github.com/user-attachments/assets/3e3ea480-3584-4dd8-8ee9-5a64fb038fd2)


## Заполнение Реализации товаров и услуг

Если в документе Реализации товаров и услуг выбран договор с видом абонентская плата, то необходимо реализовать возможность автозаполнения такого документа суммой ежемесячной абонентской платы и суммой за выполненные в течения месяца работы по данным документов Обслуживание клиентов.
Из документа должен печататься акт об оказанных услугах.

## Реализованное решение

1. Добавлены константы НоменклатураАбонентскаяПлата и НоменклатураРаботыСпециалиста с типом ссылка на справочник Номенклатура
2. Добавлена на форму Реализации товаров и услуг команда Заполнить
3. Команда Заполнить проверяет вид договора. Если это договор абонентского обслуживания, то вызывает процедуру ВыполнитьАвтозаполнение из модуля объекта документа
4. В модуле объекта реализована процедура ВыполнитьАвтозаполнение, которая:
    - получает номенклатуру из констант НоменклатураАбонентскаяПлата и НоменклатураРаботыСпециалиста, если хотя бы одна не заполнена, выдает ошибку и прекращает выполнение процедуры,
    - очищает табличную часть.

      Если в договоре ненулевая сумма абонентской платы, добавлена в табличную часть строка с номенклатурой из константы НоменклатураАбонентскаяПлата и суммой абонентской платы из договора.

      Если в месяц даты документа в регистре ВыполненныеКлиентуРаботы есть информация о выполненных работах по этому договору, добавляется в табличную часть строка с номенклатурой из константы НоменклатураРаботыСпециалиста и общим количеством и суммой из регистра ВыполненныеКлиентуРаботы за месяц документа.

<details>
	
 <summary><u>Процедура ВыполнитьАвтозаполнение() в модуле объекта</u></summary>

```bsl
	// {{ Слукина М.Н.: Необходимо реализовать процедуру ВыполнитьАвтозаполнение 
	
Процедура ВыполнитьАвтозаполнение() Экспорт
	
	НоменклатураАбонентскаяПлата = Константы.ВКМ_НоменклатураАбонентскаяПлата.Получить();
	НоменклатураРаботыСпециалиста = Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить();
	
	Если Не ЗначениеЗаполнено(НоменклатураАбонентскаяПлата) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Необходимо заполнить константу номенклатура абонентская плата");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НоменклатураРаботыСпециалиста) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Необходимо заполнить константу номенклатура работы специалиста");
		Возврат;
	КонецЕсли;

	Товары.Очистить();
	Услуги.Очистить();
	
Запрос = Новый Запрос;
Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_ВыполненныеКлиентуРаботыОбороты.Клиент,
	|	ВКМ_ВыполненныеКлиентуРаботыОбороты.Договор,
	|	СУММА(ЕСТЬNULL(ВКМ_ВыполненныеКлиентуРаботыОбороты.КоличествоЧасовОборот, 0)) КАК КоличествоЧасов,
	|	СУММА(ЕСТЬNULL(ВКМ_ВыполненныеКлиентуРаботыОбороты.СуммаКОплатеОборот, 0)) КАК СуммаКОплате,
	|	ДоговорыКонтрагентов.ВКМ_СуммаАбонентскойПлаты КАК СуммаАбонентскойПлаты
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеКлиентуРаботы.Обороты(&НачалоПериода, &КонецПериода,,
	|			Договор = &Ссылка) КАК ВКМ_ВыполненныеКлиентуРаботыОбороты
	|		ПО ДоговорыКонтрагентов.Ссылка = ВКМ_ВыполненныеКлиентуРаботыОбороты.Договор
	|		И ДоговорыКонтрагентов.Владелец = ВКМ_ВыполненныеКлиентуРаботыОбороты.Клиент
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ВыполненныеКлиентуРаботыОбороты.Договор,
	|	ВКМ_ВыполненныеКлиентуРаботыОбороты.Клиент,
	|	ДоговорыКонтрагентов.ВКМ_СуммаАбонентскойПлаты";

Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
Запрос.УстановитьПараметр("Ссылка", Договор);


РезультатЗапроса = Запрос.Выполнить();

ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();


Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
	
		Если ВыборкаДетальныеЗаписи.СуммаАбонентскойПлаты <> 0 Тогда
			НоваяСтрокаТЧ = Услуги.Добавить();
			НоваяСтрокаТЧ.Номенклатура = НоменклатураАбонентскаяПлата;
			НоваяСтрокаТЧ.Количество = 1;
			НоваяСтрокаТЧ.Сумма = ВыборкаДетальныеЗаписи.СуммаАбонентскойПлаты;
		КонецЕсли;
	
		Если ВыборкаДетальныеЗаписи.КоличествоЧасов <> 0 Тогда
			НоваяСтрокаТЧ = Услуги.Добавить();
			НоваяСтрокаТЧ.Номенклатура = НоменклатураРаботыСпециалиста;
			НоваяСтрокаТЧ.Количество = ВыборкаДетальныеЗаписи.КоличествоЧасов;
			НоваяСтрокаТЧ.Сумма = ВыборкаДетальныеЗаписи.СуммаКОплате;
		КонецЕсли;

КонецЦикла;

КонецПроцедуры

//}}
```
</details>

5. Реализована для документа печатная форма акта об оказанных услугах. Акт содержит следующие данные:
    - номер и дату документа,
    - информацию о Контрагенте, Организации и договоре,
    - детализацию из табличной части документа (номенклатуру, количество, цену, сумму),
    - итоговую сумму цифрами и прописью,
    - поля для подписей ответственных лиц и печати.

![image](https://github.com/user-attachments/assets/fb7ed54f-1978-4192-be95-54839d00a312)

<details>
	
 <summary><u>Модуль менеджера документа Реализация</u></summary>

```bsl
//{{ Слукина М.Н. добавление команды печати и акта об оказании услуг в документ

Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт об оказании услуг
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Акт";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказании услуг'");
	КомандаПечати.Порядок = 5;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Акт");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАкта(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт об оказании услуг'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ВКМ_ПФ_MXL_АктОбОказанныхУслугах";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьАкта (МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Акт";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ВКМ_ПФ_MXL_АктОбОказанныхУслугах");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
		ВывестиОбластьШапка(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиКонтрагентОрганизацияДоговор(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиТоварыУслуги(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиПодписи (ДанныеДокументов, ТабличныйДокумент, Макет);
	
		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ВывестиОбластьШапка(ДанныеДокументов, ТабличныйДокумент, Макет)
	ОбластьШапка = Макет.ПолучитьОбласть ("Шапка"); 
	ОбластьШапка.Параметры.ДатаДокумента = Формат (ДанныеДокументов.Дата, "ДФ=dd.MM.yyyy") + " г.";
	ОбластьШапка.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер);
	ТабличныйДокумент.Вывести(ОбластьШапка);
КонецПроцедуры

Процедура ВывестиКонтрагентОрганизацияДоговор(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьКонтрагент = Макет.ПолучитьОбласть("Контрагент");
	ОбластьОрганизация = Макет.ПолучитьОбласть("Организация");
	ОбластьДоговор = Макет.ПолучитьОбласть("Договор");
	
	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("ПредставлениеКонтрагента", ДанныеДокументов.Контрагент);
	ДанныеПечати.Вставить("ПредставлениеОрганизации", ДанныеДокументов.Организация);
	ДанныеПечати.Вставить("Договор", ДанныеДокументов.Договор);
	
	ОбластьКонтрагент.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьКонтрагент);
	
	ОбластьОрганизация.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьОрганизация);
	
	ОбластьДоговор.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьДоговор);
	
КонецПроцедуры

Процедура ВывестиТоварыУслуги(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьШапкаТЧ = Макет.ПолучитьОбласть("ШапкаТЧ");
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаТЧ");
	ОбластьВсего = Макет.ПолучитьОбласть("Всего");
	ОбластьСуммаПрописью = Макет.ПолучитьОбласть("СуммаПрописью");
	
	ТабличныйДокумент.Вывести(ОбластьШапкаТЧ);
	
	ВыборкаТовары = ДанныеДокументов.Товары.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	ВыборкаУслуги = ДанныеДокументов.Услуги.Выбрать();
	Пока ВыборкаУслуги.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(ВыборкаУслуги);
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("Всего", ДанныеДокументов.СуммаДокумента);
	
	ОбластьВсего.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьВсего);
	
	ДанныеСуммы = Новый Структура;
	ДанныеСуммы.Вставить ("СуммаПрописью", ЧислоПрописью(ДанныеДокументов.СуммаДокумента, "Л=ru_RU; ДП = Ложь", "рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2"));
	
	ОбластьСуммаПрописью.Параметры.Заполнить(ДанныеСуммы);
	ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
	
КонецПроцедуры

Процедура ВывестиПодписи(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
	ОбластьПодписи.Параметры.Контрагента = ДанныеДокументов.Контрагент;
	ОбластьПодписи.Параметры.Организации = ДанныеДокументов.Организация;

	ТабличныйДокумент.Вывести(ОбластьПодписи);
	
КонецПроцедуры

Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Номер,
	|	РеализацияТоваровУслуг.Дата,
	|	РеализацияТоваровУслуг.Организация,
	|	РеализацияТоваровУслуг.Контрагент,
	|	РеализацияТоваровУслуг.Договор,
	|	РеализацияТоваровУслуг.СуммаДокумента,
	|	РеализацияТоваровУслуг.Ответственный,
	|	РеализацияТоваровУслуг.Товары.(
	|		Ссылка,
	|		НомерСтроки,
	|		Номенклатура,
	|		Количество,
	|		Цена,
	|		Сумма),
	|	РеализацияТоваровУслуг.Услуги.(
	|		Ссылка,
	|		НомерСтроки,
	|		Номенклатура,
	|		Количество,
	|		Цена,
	|		Сумма)
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В (&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

#КонецОбласти
//}}
```
</details>

## Массовое создание документов Реализация товаров и услуг

В начале месяца бухгалтер фирмы формирует акты по всем абонентским договорам. Необходимо автоматизировать эту процедуру.

## Реализованное решение
1. Создана обработка МассовоеСозданиеАктов
2. Добавлен реквизит обработки Период для указания месяца
3. Добавлена табличная часть для хранения списка договоров и ссылок на созданные Реализации по этим договорам за период
4. Реализован интерфейс для работы с обработкой

![image](https://github.com/user-attachments/assets/0d9fc9e3-87a8-47c8-8a62-7131d2578151)

5. Реализован алгоритм заполнения табличной части и создания реализаций со следующими особенностями:
    - алгоритм выполняется с использованием механизма длительных операций БСП.

<details>
	
 <summary><u>Модуль формы обработки</u></summary>

```bsl
#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	Объект.СписокДоговоров.Очистить();
	Индикатор = 0;
	СтрокаСостояния = "";
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьРеализации(Команда)
	
	Объект.СписокДоговоров.Очистить();
	//заполняем ПараметрыЗапуска  для структуры фонового задания, 
	//задаем вывод ПрогрессаВыполнения с интервалом на форму длительной операции. 
	//"активируем" фоновое задание и "отслеживаем его" с помощью ДлительныеОперацииКлиент.ОжидатьЗавершение().
	ИдЗадания = "";
	Индикатор = 0;
	СтрокаСостояния = "";
	
	ПараметрыЗапуска = Новый Структура();
	ПараметрыЗапуска.Вставить("НачалоПериода", Объект.Период.ДатаНачала);
	ПараметрыЗапуска.Вставить("КонецПериода",Объект.Период.ДатаОкончания);
	
	СтруктураФоновогоЗадания = ВыполнитьФоновоеЗаданиеНаСервере (ПараметрыЗапуска, УникальныйИдентификатор);
	ИдЗадания = СтруктураФоновогоЗадания.ИдентификаторЗадания;
//	готовим параметры ожидания, как именно будем ждать нашу форму
//	сделаем коллекцию параметры ожиданич, передаем туда нашу форму
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	
//	выведем прогресс
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.Интервал = 2;
	
//	ждем, создаем описание оповещения, указываем, чтометод находится в нашей форме
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОповещениеОЗавершении", ЭтотОбъект);
	
//	Начинаем ждать завершения, передаем что именно ждем (операция),
//	куда должны попасть при проверке (оповещение о завершении, и как именно ждем (параметры ожидания)
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(СтруктураФоновогоЗадания,ОповещениеОЗавершении,ПараметрыОжидания);
КонецПроцедуры 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
//реализуем метод Оповещение о завершении на форме, вызывается при завершении фонового задания
//он экспортный, клиентский, 
//получает 2 параметра Результат и доп.параметры

&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт
	
	//ОтключитьОбработчикОжидания("ОбработчикОжиданияИндикатор");
	//результат - коллекция, в которой указан адрес и статус (выполняется, отменено или выполнено)
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		 ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
		 СтрокаСостояния = "Ошибка";
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		Индикатор = 100;
		СтрокаСостояния = "Выполнено";
		ЗанестиВТЧ (ПолучитьИзВременногоХранилища(Результат.АдресРезультата));
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ВыполнитьФоновоеЗаданиеНаСервере (ПараметрыЗапуска, УникальныйИдентификатор)
	
	НаименованиеЗадания = "Создать акты для договоров с абонентским обслуживанием";
	
	ВыполняемыйМетод = "ВКМ_ДлительныйМодуль.СоздатьРеализации";
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	//гоовим длительную операцию, передаем уникальный идентификатор формы, чтобы БСП знала куда оповещать
	//имя функции в виде строки где лежит код (в обработках в модуле менеджера)
	СтруктураФоновогоЗадания  = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ВыполняемыйМетод, ПараметрыЗапуска.НачалоПериода, ПараметрыЗапуска.КонецПериода);
	
	Возврат СтруктураФоновогоЗадания;
		
КонецФункции

&НаКлиенте 
Процедура ОбработчикОжиданияИндикатор() 
	
	Прогресс = ПрочитатьПрогресс(ИдЗадания);
	Если НЕ ТипЗнч(Прогресс) = Тип("Структура") Тогда
		СтрокаСостояния = "";
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Прогресс) = Тип("Структура") 
		И Прогресс.Свойство("ЗавершеноАварийно") Тогда
		ОтключитьОбработчикОжидания("ОбработчикОжиданияИндикатор");
		Возврат;
	КонецЕсли;
	
	Если Прогресс.Свойство("ЗаданиеВыполнено") И Прогресс.ЗаданиеВыполнено Тогда
		Индикатор = 100;
		СтрокаСостояния = "Задание завершено.";
	Иначе
		Если Прогресс.Свойство("Процент") Тогда
			Индикатор = Прогресс.Процент;
			
		КонецЕсли;
		
		Если Прогресс.Свойство("Текст") Тогда
			СтрокаСостояния = Прогресс.Текст;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПрочитатьПрогресс(Знач ИдентификаторФоновогоЗадания)
	
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторФоновогоЗадания);
	
	Если Задание = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Задание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
		СтруктураОтвета = Новый Структура;
		СтруктураОтвета.Вставить("ЗавершеноАварийно", Истина);
		Возврат СтруктураОтвета;
	КонецЕсли;
	
	// используем БСП
	ПрогрессЗадания = ДлительныеОперации.ПрочитатьПрогресс(ИдентификаторФоновогоЗадания);
	
	Если ПрогрессЗадания = Неопределено
	 Или ТипЗнч(ПрогрессЗадания) <> Тип("Структура") Тогда 
		ПрогрессЗадания = Новый Структура;
	КонецЕсли;
	ПрогрессЗадания.Вставить("ЗаданиеВыполнено", ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторФоновогоЗадания));
	
	Возврат ПрогрессЗадания;
	
КонецФункции

&НаКлиенте 
Процедура ЗанестиВТЧ(Результат) 
	
	Для Каждого ДанныеСтроки из Результат Цикл
	
		НоваяСтрокаТЧ = Объект.СписокДоговоров.Добавить();
		НоваяСтрокаТЧ.Договор = ДанныеСтроки.Договор;
		НоваяСтрокаТЧ.Реализация = ДанныеСтроки.Реализация;
		
	
	КонецЦикла;

КонецПроцедуры
#КонецОбласти
```
</details>

- если Реализация за выбранный месяц уже создана, то обработка не создает вторую,
- при создании Реализации вызывается стандартный алгоритм заполнения,
- для заполнения Реализации используется метод модуля объекта Реализации ВыполнитьАвтозаполнение,
- перед проведением Реализации вызывается стандартный алгоритм проверки заполнения.


<details>
	
 <summary><u>Общий модуль ВКМ_ДлительныйМодуль</u></summary>

```bsl
    #Область СлужебныеПроцедурыИФункции

Функция СоздатьРеализации (НачалоПериода, КонецПериода) Экспорт
	МассивВозврат = Новый Массив;
	
	СозданныеРеализации = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	|	ДоговорыКонтрагентов.ВКМ_ДатаОкончания КАК ВКМ_ДатаОкончания,
	|	ДоговорыКонтрагентов.ВКМ_ДатаНачала
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО ДоговорыКонтрагентов.Ссылка = РеализацияТоваровУслуг.Договор
	|		И РеализацияТоваровУслуг.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаНачалаПериода, МЕСЯЦ) И КОНЕЦПЕРИОДА(&ДатаОкончанияПериода,
	|			МЕСЯЦ)
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
	|	И (ДоговорыКонтрагентов.ВКМ_ДатаНачала <= &ДатаОкончанияПериода
	|	И ДоговорыКонтрагентов.ВКМ_ДатаОкончания >= &ДатаНачалаПериода)
	|	И ДоговорыКонтрагентов.ПометкаУдаления = ЛОЖЬ
	|	И РеализацияТоваровУслуг.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ВидДоговора", 	Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
	Запрос.УстановитьПараметр("ДатаОкончанияПериода", КонецПериода);
	Запрос.УстановитьПараметр("ДатаНачалаПериода", НачалоПериода);
	
	ДоговорыВыборка = Запрос.Выполнить().Выгрузить();
	ВсегоДоговоров = ДоговорыВыборка.Количество();
	ТекущийДоговор = 1;
	
	Для каждого Договор из ДоговорыВыборка Цикл
		ДокументРеализацияОбъект = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		ДокументРеализацияОбъект.Дата = НачалоПериода;
		ДокументРеализацияОбъект.Заполнить(Договор.Ссылка);
		ДокументРеализацияОбъект.ВыполнитьАвтозаполнение();
		ДокументРеализацияОбъект.ПроверитьЗаполнение();
		ДокументРеализацияОбъект.Записать(РежимЗаписиДокумента.Проведение);;

		ПроцентВыполнения = (ТекущийДоговор/ВсегоДоговоров)*100;
		ПроцентВыполнения = Окр (ПроцентВыполнения, 0);
		
		СозданныеРеализации.Вставить("Договор", Договор.Ссылка);
		СозданныеРеализации.Вставить("Реализация", ДокументРеализацияОбъект.Ссылка);
		
		МассивВозврат.Добавить(СозданныеРеализации);
		
		ДлительныеОперации.СообщитьПрогресс(ПроцентВыполнения,СокрЛП(ДокументРеализацияОбъект.Ссылка));
		
		ТекущийДоговор = ТекущийДоговор + 1;
		
	КонецЦикла;

Возврат МассивВозврат;

КонецФункции
#КонецОбласти
```
</details>

5. Добавлена обработка в подсистему Обслуживание клиентов

   
## Права доступа

Документы Обслуживание клиентов вводят специалисты и менджеры. Массовое создание документов Реализации использует бухгалтер. Для настройкии прав доступа добавляются поставляемые профили групп доступа и роли для работы с объектами

## Реализованное решение

<details>
	
 <summary><u>ОМ УправлениеДоступомИТ</u></summary>

```bsl
	//Слукина М.Н.: 
	//Добавление профилей  групп доступа Специалист, Менеджер, БухгалтерИТФирмы
	//для роли обслуживание клиентов профиль специалист и менеджер
	//для роли массовое создание реализаций - бухгалтеор
	


	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя = "ВКМ_Бухгалтер";
	ОписаниеПрофиля.Наименование  = НСтр("ru = 'ВКМ Бухгалтер'", ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Идентификатор = "a2b0dd48-ab6d-49d6-92ff-0dd0311c603f";
	ОписаниеПрофиля.Роли.Добавить("ЗапускВебКлиента");
	ОписаниеПрофиля.Роли.Добавить("ЗапускТонкогоКлиента");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваБСП");
	ОписаниеПрофиля.Роли.Добавить("ВыводНаПринтерФайлБуферОбмена");
	ОписаниеПрофиля.Роли.Добавить("ПросмотрСвязанныеДокументы");
	ОписаниеПрофиля.Роли.Добавить("СохранениеДанныхПользователя");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеВариантовОтчетов");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеДополнительныхОтчетовИОбработок");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеДополнительныхСведений");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеУчетныхЗаписейЭлектроннойПочты");
	ОписаниеПрофиля.Роли.Добавить("БазоваяФункциональностьТП");
	ОписаниеПрофиля.Роли.Добавить("ВКМ_Бухгалтерия");
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя = "ВКМ_Кадровик-Расчетчик";
	ОписаниеПрофиля.Наименование  = НСтр("ru = 'ВКМ Кадровик расчетчик'", ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Идентификатор = "afe72ace-2ad2-41e4-ab50-9ab8111c7139";
	ОписаниеПрофиля.Роли.Добавить("ЗапускВебКлиента");
	ОписаниеПрофиля.Роли.Добавить("ЗапускТонкогоКлиента");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваБСП");
	ОписаниеПрофиля.Роли.Добавить("ВыводНаПринтерФайлБуферОбмена");
	ОписаниеПрофиля.Роли.Добавить("ПросмотрСвязанныеДокументы");
	ОписаниеПрофиля.Роли.Добавить("СохранениеДанныхПользователя");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеВариантовОтчетов");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеДополнительныхОтчетовИОбработок");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеДополнительныхСведений");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеУчетныхЗаписейЭлектроннойПочты");
	ОписаниеПрофиля.Роли.Добавить("БазоваяФункциональностьТП");
	ОписаниеПрофиля.Роли.Добавить("ВКМ_КадровикРасчетчик");
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя = "ВКМ_Менеджер";
	ОписаниеПрофиля.Наименование  = НСтр("ru = 'ВКМ Менеджер'", ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Идентификатор = "40f7355a-b1f6-4279-b9cd-7a5a07a19b0b";
	ОписаниеПрофиля.Роли.Добавить("ЗапускВебКлиента");
	ОписаниеПрофиля.Роли.Добавить("ЗапускТонкогоКлиента");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваБСП");
	ОписаниеПрофиля.Роли.Добавить("ВыводНаПринтерФайлБуферОбмена");
	ОписаниеПрофиля.Роли.Добавить("ПросмотрСвязанныеДокументы");
	ОписаниеПрофиля.Роли.Добавить("СохранениеДанныхПользователя");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеВариантовОтчетов");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеДополнительныхОтчетовИОбработок");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеДополнительныхСведений");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеУчетныхЗаписейЭлектроннойПочты");
	ОписаниеПрофиля.Роли.Добавить("БазоваяФункциональностьТП");
	ОписаниеПрофиля.Роли.Добавить("ВКМ_ОбслуживаниеКлиентов");
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);

	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя = "ВКМ_Специалист";
	ОписаниеПрофиля.Наименование  = НСтр("ru = 'ВКМ Специалист'", ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Идентификатор = "90911600-b4d8-4232-a089-c77d632ad6f8";
	ОписаниеПрофиля.Роли.Добавить("ЗапускВебКлиента");
	ОписаниеПрофиля.Роли.Добавить("ЗапускТонкогоКлиента");
	ОписаниеПрофиля.Роли.Добавить("БазовыеПраваБСП");
	ОписаниеПрофиля.Роли.Добавить("ВыводНаПринтерФайлБуферОбмена");
	ОписаниеПрофиля.Роли.Добавить("ПросмотрСвязанныеДокументы");
	ОписаниеПрофиля.Роли.Добавить("СохранениеДанныхПользователя");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеВариантовОтчетов");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеДополнительныхОтчетовИОбработок");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеДополнительныхСведений");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеУчетныхЗаписейЭлектроннойПочты");
	ОписаниеПрофиля.Роли.Добавить("БазоваяФункциональностьТП");
	ОписаниеПрофиля.Роли.Добавить("ВКМ_ОбслуживаниеКлиентов");
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
	//
КонецПроцедуры
```
</details>
