# Расчёт управленческой зарплаты

На предприятии работают сотрудники двух категорий: специалисты и административный персонал. Специалистам начисляется минимальный оклад и процент за выполненные работы клиентам. Административный персонал получает оклад. Любому сотруднику на усмотрение руководства может быть начислена разовая фиксированная премия.

Сотрудники могут уходить в отпуск. В период отпуска оклад не начисляется. Отпускные рассчитываются из расчёта среднедневной заработной платы за последние 12 месяцев:

`(Начисленная сумма за 12 мес. / Количество рабочих дней) х Количество календарных дней отпуска`

С каждого сотрудника удерживается НДФЛ – 13%.

Для упрощения примем, что выплаты осуществляются один раз в месяц после начисления зарплаты. Кроме этого на предприятии планируются отпуска на год вперёд.

## Расчёт зарплаты

Для каждого сотрудника необходимо хранить суммы окладов. Дополнительно для специалистов необходимо хранить процент от выполненных заказов, который им будет начисляться. Расчёт зарплаты выполняется документом НачислениеЗаработнойПлаты по всем видам расчёта, кроме премий. Премии начисляются отдельным документом НачислениеФиксированныхПремий.

В документах НачислениеЗаработнойПлаты и НачислениеФиксированныхПремий должно быть автоматически рассчитано удержание НДФЛ.

**Реализованное решение**
1. Добавлены планы видов расчёта ОсновныеНачисления, ДополнительныеНачисления и Удержания. Настроены с учётом описанного сценария начислений
2. Созданы регистры расчёта ОсновныеНачисления, ДополнительныеНачисления и Удержания
3. Создан регистр сведений УсловияОплатыСотрудников с периодичностью Месяц, измерением Сотрудник и ресурсами Оклад и ПроцентОтРабот.
4. Создан регистр накопления ВыполненныеСотрудникомРаботы с измерением Сотрудник, ресурсами ЧасовКОплате и СуммаКОплате
5. Реализован в документе ОбслуживаниеКлиента алгоритм для формирования движений по регистру ВыполненныеСотрудникомРаботы.

    Если на дату документа специалисту не установлен процент оплаты от работ, то документ не проводится. Если установлен нулевой процент, в этом случае документ проводится.

   СуммаКОплате рассчитывается по формуе:

   `ЧасовКОплатеКлиенту * СтавкаЧасаКлиента * ПроцентОтРабот / 100`
```bsl
   Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записать();
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Дата КАК Дата,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_ДатаНачала КАК ДатаНачалаДоговора,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_ДатаОкончания КАК ДатаОкончанияДоговора,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВидДоговора КАК ВидДоговора,
		|	СУММА(ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.ЧасыКОплатеКлиенту) КАК КоличествоЧасов,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Клиент КАК Клиент,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор КАК Договор,
		|	СУММА(ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Сумма) КАК СуммаКОплате,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Специалист КАК Сотрудник,
		|	МАКСИМУМ(ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот) КАК ПроцентОтРабот,
		|	МАКСИМУМ(ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_СтоимостьЧасаРаботы) КАК СтоимостьЧасаРаботы,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_СуммаАбонентскойПлаты КАК СуммаАбонентскойПлаты
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента.ВыполненныеРаботы КАК ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, Сотрудник В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВКМ_ОбслуживаниеКлиента.Специалист
		|			ИЗ
		|				Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Специалист = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Дата,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_ДатаНачала,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_ДатаОкончания,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВидДоговора,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Клиент,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Специалист,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка.Договор.ВКМ_СуммаАбонентскойПлаты";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
		Если ВыборкаДетальныеЗаписи.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда 
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Вид договора не является абонентским обслуживанием");
			Прервать;
		КонецЕсли;
		
		Если ВыборкаДетальныеЗаписи.Дата > КонецДня(ВыборкаДетальныеЗаписи.ДатаОкончанияДоговора)
			Или ВыборкаДетальныеЗаписи.Дата < ВыборкаДетальныеЗаписи.ДатаНачалаДоговора
			Тогда
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Дата документа должна быть в границах действия договора на абоненсткое обслуживание!");
			Прервать;
		КонецЕсли;	
		
		Если ПустаяСтрока(ВыборкаДетальныеЗаписи.ПроцентОтРабот) Тогда
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Специалисту не установлен процент оплаты от работ!");
			Прервать;
		КонецЕсли;	
		
		//регистр ВКМ_ВыполненныеКлиентуРаботы
		Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
		Движение.Период = Дата;
		
		//регистр ВКМ_ВыполненныеСотрудникомРаботы
		Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
		Движение.Период = Дата;
		Движение.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
		Движение.ЧасовКОплате = ВыборкаДетальныеЗаписи.КоличествоЧасов;
		Движение.Процент = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ВыборкаДетальныеЗаписи.ПроцентОтРабот);
		Движение.СуммаКОплате = ВыборкаДетальныеЗаписи.КоличествоЧасов * ВыборкаДетальныеЗаписи.СтоимостьЧасаРаботы * Движение.Процент / 100;

			
	КонецЦикла;


КонецПроцедуры
```

   
7. Создан документ НачисленияЗарплаты и реализован алгоритм расчёта в соответствии с требованиями к механизмам сложных периодических расчётов для экзамена 1С:Специалист по платформе.

Модуль объекта документа ВКМ_НачислениеЗарплаты

```bsl
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвижения();

	СформироватьУдержания();
	
	РассчитатьОклад(); 
	
	РассчитатьОпускные();
	
	РассчитатьПремию (Отказ);

	ВКМ_РасчетныйМодуль.РассчитатьНДФЛ(Ссылка);
	
	ВКМ_РасчетныйМодуль.СформироватьДвиженияВзаиморасчетыССотрудниками (Ссылка);

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвижения()
	
	Для каждого Строка из ОсновныеНачисления Цикл  
		
		//переносим данные по основным начислениям из ТЧ в регистр

			Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.ВидРасчета = Строка.ВидРасчета; 
			Движение.ПериодДействияНачало = Строка.ДатаНачала;
			Движение.ПериодДействияКонец = Строка.ДатаОкончания;
			Движение.Сотрудник = Строка.Сотрудник;
			Движение.Подразделение = Строка.Подразделение; 
			Движение.ГрафикРаботы = Строка.ГрафикРаботы;
			
			Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
				
				Движение.БазовыйПериодНачало = НачалоМесяца(ДобавитьМесяц(Строка.ДатаНачала, -12)); 
				Движение.БазовыйПериодКонец = КонецМесяца(ДобавитьМесяц(Строка.ДатаОкончания, -1)); 
				Движение.Дней = День (Строка.ДатаОкончания) - День (Строка.ДатаНачала) + 1;  //сколько фактически дней чел был в отпуске   
				
			КонецЕсли;
		
	КонецЦикла; 

	Для каждого Строка из ДополнительныеНачисления Цикл  
		
		//переносим данные по доп.начислениям из ТЧ в регистр
			Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.ВидРасчета = Строка.ВидРасчета;
			Движение.Сотрудник = Строка.Сотрудник;
			Движение.Подразделение = Строка.Подразделение; 
		
	КонецЦикла; 
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	
КонецПроцедуры  

Процедура СформироватьУдержания()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_НачислениеЗарплатыОсновныеНачисления.Сотрудник КАК Сотрудник,
	|	ВКМ_НачислениеЗарплатыОсновныеНачисления.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	Документ.ВКМ_НачислениеЗарплаты.ОсновныеНачисления КАК ВКМ_НачислениеЗарплатыОсновныеНачисления
	|ГДЕ
	|	ВКМ_НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_НачислениеЗарплатыОсновныеНачисления.Сотрудник,
	|	ВКМ_НачислениеЗарплатыОсновныеНачисления.Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВКМ_НачислениеЗарплатыДополнительныеНачисления.Сотрудник,
	|	ВКМ_НачислениеЗарплатыДополнительныеНачисления.Подразделение
	|ИЗ
	|	Документ.ВКМ_НачислениеЗарплаты.ДополнительныеНачисления КАК ВКМ_НачислениеЗарплатыДополнительныеНачисления
	|ГДЕ
	|	ВКМ_НачислениеЗарплатыДополнительныеНачисления.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_НачислениеЗарплатыДополнительныеНачисления.Подразделение,
	|	ВКМ_НачислениеЗарплатыДополнительныеНачисления.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Подразделение КАК Подразделение,
	|	ВТ.Сотрудник КАК Сотрудник
	|ИЗ
	|	ВТ КАК ВТ
	|СГРУППИРОВАТЬ ПО
	|	ВТ.Подразделение,
	|	ВТ.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
			Движение = Движения.ВКМ_Удержания.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
			Движение.БазовыйПериодКонец = КонецМесяца(Дата);
			Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
			Движение.Подразделение = ВыборкаДетальныеЗаписи.Подразделение;
			Движение.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;

	КонецЦикла;

	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры  

Процедура РассчитатьОклад()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ФактическиОтработаноДней,
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия КАК КоличествоРабочихДнейВПериоде,
		|	ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад, 0) КАК Оклад
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(ВидРасчета = &Оклад
		|	И Регистратор = &Ссылка) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, (Подразделение, Сотрудник) В
		|			(ВЫБРАТЬ
		|				ВКМ_ОсновныеНачисления.Подразделение,
		|				ВКМ_ОсновныеНачисления.Сотрудник
		|			ИЗ
		|				РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|			ГДЕ
		|				ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
		|				И ВКМ_ОсновныеНачисления.ВидРасчета = &Оклад)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО ВКМ_УсловияОплатыСотрудниковСрезПоследних.Подразделение = ВКМ_ОсновныеНачисленияДанныеГрафика.Подразделение
		|		И ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник = ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник";
	
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1]; 
		
		Движение.Показатель = ВыборкаДетальныеЗаписи.Оклад;
		Движение.Дней = ВыборкаДетальныеЗаписи.ФактическиОтработаноДней;
		Движение.Результат = Движение.Показатель * ВыборкаДетальныеЗаписи.ФактическиОтработаноДней / ВыборкаДетальныеЗаписи.КоличествоРабочихДнейВПериоде; 

	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
КонецПроцедуры  


Процедура РассчитатьОпускные()
	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.НомерСтроки,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) КАК БазаОсн,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ДнейБаза, 0) КАК ОтработаноДней,
		|	ЕСТЬNULL(ВКМ_ДополнительныеНачисленияБазаВКМ_ДополнительныеНачисления.РезультатБаза, 0) КАК БазаДоп
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения,,
		|			Регистратор = &Ссылка
		|		И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ДополнительныеНачисления.БазаВКМ_ДополнительныеНачисления(&Измерения,
		|			&Измерения,, Регистратор = &Ссылка
		|		И ВидРасчета = &Отпуск) КАК ВКМ_ДополнительныеНачисленияБазаВКМ_ДополнительныеНачисления
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
		|	И ВКМ_ОсновныеНачисления.ВидРасчета = &Отпуск";
	

	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Измерения = Новый Массив;
	Измерения.Добавить("Подразделение");
	Измерения.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1];
		
		Если ВыборкаДетальныеЗаписи.ОтработаноДней = 0 Тогда
			Движение.Результат = 0;
			Продолжить;  
		КонецЕсли;
		
		Движение.Показатель = (ВыборкаДетальныеЗаписи.БазаОсн + ВыборкаДетальныеЗаписи.БазаДоп) / ВыборкаДетальныеЗаписи.ОтработаноДней; //средняя дневная ставка 
		Движение.Результат =  Движение.Показатель * Движение.Дней;  //средняя дневная ставка на количество дней отпуска
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
КонецПроцедуры  


Процедура РассчитатьПремию(Отказ)
	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеОборот, 0) КАК СуммаКОплатеОборот,
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот КАК ПроцентОтРабот,
		|	ВКМ_ДополнительныеНачисления.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&НачалоПериода, &ОкончаниеПериода,,
		|			Сотрудник В
		|			(ВЫБРАТЬ
		|				ВКМ_НачислениеЗарплатыДополнительныеНачисления.Сотрудник
		|			ИЗ
		|				Документ.ВКМ_НачислениеЗарплаты.ДополнительныеНачисления КАК ВКМ_НачислениеЗарплатыДополнительныеНачисления
		|			ГДЕ
		|				ВКМ_НачислениеЗарплатыДополнительныеНачисления.Ссылка = &Ссылка)) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты
		|		ПО (ВКМ_ДополнительныеНачисления.Сотрудник = ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&НачалоПериода, Сотрудник В
		|			(ВЫБРАТЬ
		|				ВКМ_ДополнительныеНачисления.Сотрудник
		|			ИЗ
		|				РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
		|			ГДЕ
		|				ВКМ_ДополнительныеНачисления.Регистратор = &Ссылка
		|				И ВКМ_ДополнительныеНачисления.ВидРасчета = &ПремияПоПроценту)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО (ВКМ_ДополнительныеНачисления.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник)
		|ГДЕ
		|	ВКМ_ДополнительныеНачисления.Регистратор = &Ссылка
		|	И ВКМ_ДополнительныеНачисления.ВидРасчета = &ПремияПоПроценту";
	

	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(Дата));

	Запрос.УстановитьПараметр("ПремияПоПроценту", ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ПремияПроцентом);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ПустаяСтрока(ВыборкаДетальныеЗаписи.ПроцентОтРабот) Тогда
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Специалисту %1 не установлен процент оплаты от работ!", ВыборкаДетальныеЗаписи.Сотрудник));
			Прервать;
		КонецЕсли;	
		
		Движение = Движения.ВКМ_ДополнительныеНачисления[ВыборкаДетальныеЗаписи.НомерСтроки - 1];
		
		Движение.Размер =  СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ВыборкаДетальныеЗаписи.ПроцентОтРабот); 
		Движение.Результат =  ВыборкаДетальныеЗаписи.СуммаКОплатеОборот;
		
	КонецЦикла;
	
	Движения.ВКМ_ДополнительныеНачисления.Записать(, Истина);
КонецПроцедуры  

#КонецОбласти

#КонецЕсли
```

8. Создан документ НачислениеФиксированнойПремии с табличной частью СписокСотрудников (Сотрудник, СуммаПремии), реализована запись премии в регистр при проведении и расчёте НДФЛ.

```bsl
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвижения();

	СформироватьУдержания();

	ВКМ_РасчетныйМодуль.РассчитатьНДФЛ (Ссылка);
	
	ВКМ_РасчетныйМодуль.СформироватьДвиженияВзаиморасчетыССотрудниками(Ссылка);

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвижения()
	
	Для каждого Строка из СписокСотрудников Цикл  
		
		//переносим данные по доп.начислениям из ТЧ в регистр
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ПремияСуммой;
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.Подразделение = Строка.Подразделение; 
		Движение.Результат = Строка.СуммаПремия;
		
	КонецЦикла; 
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	
КонецПроцедуры  

Процедура СформироватьУдержания()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Подразделение,
	|	ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Сотрудник,
	|	ЕСТЬNULL(СУММА(ВКМ_НачислениеФиксированнойПремииСписокСотрудников.СуммаПремия), 0) КАК СуммаПремия
	|ИЗ
	|	Документ.ВКМ_НачислениеФиксированнойПремии.СписокСотрудников КАК ВКМ_НачислениеФиксированнойПремииСписокСотрудников
	|ГДЕ
	|	ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Подразделение,
	|	ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
			Движение = Движения.ВКМ_Удержания.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
			Движение.БазовыйПериодКонец = КонецМесяца(Дата);
			Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
			Движение.Подразделение = ВыборкаДетальныеЗаписи.Подразделение;
			Движение.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;

	КонецЦикла;

	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры 
#КонецОбласти
 

#КонецЕсли
```


## Выплата зарплаты

Необходимо учитывать начисленные и выплаченные суммы сотрудникам.

**Реализованное решение**
1. Создан регистр накопления ВзаиморасчетыССотрудниками с измерением Сотрудник и ресурсов Сумма.
2. Для документов НачислениеЗарплаты и НачислениеФиксированнойПремии реализован алгоритм формирования движений по регистру ВзаиморасчетыССотрудниками.

Сумма должна расчитываться как

`ОсновныеНачисления + ДополнительныеНачисления - Удержания`, 

зарегистрированные этим документом.

ОМ ВКМ_РасчетныйМодуль
```bsl
#Область СлужебныеПроцедурыИФункции

Процедура РассчитатьНДФЛ(Регистратор) Экспорт
    
    НаборЗаписей = РегистрыРасчета.ВКМ_Удержания.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
			"ВЫБРАТЬ
			|	ЕСТЬNULL(ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) КАК РезультатБаза,
			|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.НомерСтроки
			|ПОМЕСТИТЬ ВТ_База
			|ИЗ
			|	РегистрРасчета.ВКМ_Удержания.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения, &Разрез, Регистратор = &Ссылка
			|	И ВидРасчета = &НДФЛ) КАК ВКМ_УдержанияБазаВКМ_ОсновныеНачисления
			|ГДЕ
			|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.РегистраторРазрез = &Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЕСТЬNULL(ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.РезультатБаза, 0) КАК РезультатБаза,
			|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.НомерСтроки
			|ИЗ
			|	РегистрРасчета.ВКМ_Удержания.БазаВКМ_ДополнительныеНачисления(&Измерения, &Измерения, &Разрез, Регистратор = &Ссылка
			|	И ВидРасчета = &НДФЛ) КАК ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления
			|ГДЕ
			|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.РегистраторРазрез = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СУММА(ВТ_База.РезультатБаза) КАК РезультатБаза,
			|	ВТ_База.НомерСтроки
			|ИЗ
			|	ВТ_База КАК ВТ_База
			|СГРУППИРОВАТЬ ПО
			|	ВТ_База.НомерСтроки";
	

	Запрос.УстановитьПараметр("Ссылка", Регистратор);
	Запрос.УстановитьПараметр("НДФЛ", ПланыВидовРасчета.ВКМ_Удержания.НДФЛ);
	
	Разрез = Новый Массив;
	Разрез.Добавить("Регистратор");	
	
	Запрос.УстановитьПараметр("Разрез", Разрез);

	
	Измерения = Новый Массив;
	Измерения.Добавить("Подразделение");	
	Измерения.Добавить("Сотрудник");

	Запрос.УстановитьПараметр("Измерения", Измерения);

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Для Каждого Строка из НаборЗаписей Цикл
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Строка.НомерСтроки, "НомерСтроки") Тогда
			Строка.Размер = ВыборкаДетальныеЗаписи.РезультатБаза;
			Строка.Результат = Строка.Размер *13 / 100;
		КонецЕсли;
		
	КонецЦикла;
	
	НаборЗаписей.Записать(, Истина);
	
КонецПроцедуры

Процедура СформироватьДвиженияВзаиморасчетыССотрудниками (Ссылка) Экспорт
	
	НаборЗаписей = РегистрыНакопления.ВКМ_ВзаиморасчетыССотрудниками.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Ссылка);
	НаборЗаписей.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|
		|	ВКМ_Удержания.Подразделение,
		|	ВКМ_Удержания.Сотрудник,
		|	СУММА(ВКМ_Удержания.Размер) КАК Начисления,
		|	СУММА(ВКМ_Удержания.Результат) КАК Удержания
		|ИЗ
		|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|ГДЕ
		|	ВКМ_Удержания.Регистратор = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_Удержания.Подразделение,
		|	ВКМ_Удержания.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = НаборЗаписей.Добавить();
		Движение.Период = Ссылка.Дата;
		Движение.Сумма = ВыборкаДетальныеЗаписи.Начисления - ВыборкаДетальныеЗаписи.Удержания;
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры
#КонецОбласти  
```

3. Создан документ ВыплатаЗарплаты:
    - с табличной частью Выплаты (Сотрудник, Сумма),
    - с механизмом автозаполнения по остаткам регистра ВзаиморасчетыССотрудниками на дату документа.
  
Модуль формы документа ВКМ_ВыплатаЗарплаты
```bsl
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Объект.Выплаты.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.Подразделение,
		|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.Сотрудник,
		|	ЕСТЬNULL(ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток, 0) КАК Сумма
		|ИЗ
		|	РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(&ПериодРегистрации,) КАК
		|		ВКМ_ВзаиморасчетыССотрудникамиОстатки
		|ГДЕ
		|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток > 0";
	
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоДня(Объект.Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НоваяСтрока = Объект.Выплаты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);

	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
```

4. Реализован механизм проведения документа с формированием движений по регистру ВзаиморасчетыССотрудниками

## Планирование отпусков

В начале года на предприятии составляется график отпусков. Необходимо реализовать возможность визуального контроля, что все сотрудники одновременно не уйдут в отпуск. 

**Реализованное решение**
1. Создан документ ГрафикОтпусков с реквизитом Год и табличной частью ОтпускаСотрудников (Сотрудник, ДатаНачала, ДатаОкончания)
2. Создана форма документа, на форме реализована подсветка строк сотрудников, общая длительность отпуска которых более 28 календарных дней

Модуль формы документа ВКМ_ГрафикОтпусков
```bsl
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПроверитьДлительностьОтпуска ();

КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтпускаСотрудников
&НаКлиенте
Процедура ОтпускаСотрудниковПриИзменении(Элемент)
	ОтпускаСотрудниковПриИзмененииНаСервере();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура АнализГрафика(Команда)
	
	АдресВремХранилища = ПоместитьВоВременноеХранТабЧастьГрафика ();
	
	ПередаваемыеПараметры = Новый Структура ("АдресВремХранилища", АдресВремХранилища);
	
	ОткрытьФорму("Документ.ВКМ_ГрафикОтпусков.Форма.АнализГрафика", ПередаваемыеПараметры, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтпускаСотрудниковПриИзмененииНаСервере()
	ПроверитьДлительностьОтпуска ();
КонецПроцедуры

&НаСервере
Процедура ПроверитьДлительностьОтпуска()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник,
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаНачала,
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ГрафикТЧДок
	|ИЗ
	|	&ГрафикОтпусков КАК ВКМ_ГрафикОтпусковОтпускаСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ГрафикТЧДок.Сотрудник,
	|	РАЗНОСТЬДАТ(ВТ_ГрафикТЧДок.ДатаНачала, ДОБАВИТЬКДАТЕ(ВТ_ГрафикТЧДок.ДатаОкончания, День, 1), День) КАК ДнейОтпуска
	|ПОМЕСТИТЬ ВТ_КоличествоДнейОтпуска
	|ИЗ
	|	ВТ_ГрафикТЧДок КАК ВТ_ГрафикТЧДок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КоличествоДнейОтпуска.Сотрудник,
	|	СУММА(ВТ_КоличествоДнейОтпуска.ДнейОтпуска) КАК ДнейОтпуска
	|ПОМЕСТИТЬ ВТ_итог
	|ИЗ
	|	ВТ_КоличествоДнейОтпуска КАК ВТ_КоличествоДнейОтпуска
	|СГРУППИРОВАТЬ ПО
	|	ВТ_КоличествоДнейОтпуска.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_итог.Сотрудник КАК Сотрудник
	|ИЗ
	|	ВТ_итог КАК ВТ_итог
	|ГДЕ
	|	ВТ_итог.ДнейОтпуска > 28";
	
	Запрос.УстановитьПараметр("ГрафикОтпусков", Объект.ОтпускаСотрудников.Выгрузить());
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
	
	Сотрудники = Новый СписокЗначений;
	Сотрудники.ЗагрузитьЗначения(Результат);
	
	УсловноеОформление.Элементы.Очистить();
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОтпускаСотрудников.Сотрудник");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ЭлементОтбора.ПравоеЗначение = Сотрудники;
	ЭлементОтбора.Использование = Истина;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", WEBЦвета.АкварельноСиний);
	
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("ОтпускаСотрудников");
	ПолеОформления.Использование = Истина;
	
	
КонецПроцедуры


&НаСервере
Функция ПоместитьВоВременноеХранТабЧастьГрафика ()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.ОтпускаСотрудников.Выгрузить());
	
КонецФункции
#КонецОбласти
```

3. Создана дополнительная форма документа АнализГрафика, добавлена команда на открытие дополнительной формы на основной форме документа

![image](https://github.com/user-attachments/assets/bfff0355-38fd-4b05-a1be-2e9c9aa595f8)


4. На форме АнализГрафика отображена диаграмма Ганта с запланированными отпусками сотрудников текущим документом в выбранном году. Данные из основной формы в дополнительную форму для построения диаграммы Ганта передаются через временное хранилище.

Модуль формы АнализГрафика документа ВКМ_ГрафикОтпусков
```bsl
#Область ОбработчикиСобытийФормы

&НаСервере

Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//получим из параметров вдрес врем.хран.
	//ранее в форме владельце вторым параметром передавали струтуру с адресом,
	//куда выгрузили таблчную часть  док.график отпусков
	//ПередаваемыеПараметры = Новый Структура ("АдресВремХранилища", АдресВремХранилища);;
	АдресВременногоХранилища = Параметры.АдресВремХранилища;
	
	//получим из врем.хранилища таблицу с графиком отпусков
	ТабличнаяЧастьОтпуска = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	//обойдем табличную часть
	
	Для Каждого Строка Из ТабличнаяЧастьОтпуска Цикл
		Точка = ГрафикОтпусков.УстановитьТочку(Строка.Сотрудник, "Сотрудник");
		Точка.Текст = Строка.Сотрудник;
		
		Серия = ГрафикОтпусков.УстановитьСерию("Отпуск");
		
		Значение = ГрафикОтпусков.ПолучитьЗначение(Точка, Серия);
		
		Интервал = Значение.Добавить();
		Интервал.Начало = Строка.ДатаНачала;
		Интервал.Конец = Строка.ДатаОкончания;
	КонецЦикла
	
КонецПроцедуры
#КонецОбласти
```
## Права доступа

Вводом информации о заработной плате, плановых отпусках занимаются кадровики-расчётчики.

**Реализовано решение**
1. Созданы необходимые роли для работы с объектами
2. Добавлен поставляемый профиль групп доступа КадровикРасчетчик
3. Добавлены в профиль роли
